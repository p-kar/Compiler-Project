<program> ===> <otherFunctions> <mainFunction>
<mainFunction> ===> TK_MAIN <stmts> TK_END
<otherFunctions> ===> <function> <otherFunctions>
<otherFunctions> ===> TK_EPS
<function> ===> TK_FUNID <input_par> <output_par> TK_SEM <stmts> TK_END
<input_par> ===> TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par> ===> TK_OUTPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par> ===> TK_EPS
<parameter_list> ===> <dataType> TK_ID <remaining_list>
<dataType> ===> <primitiveDatatype>
<dataType> ===> <constructedDatatype>
<primitiveDatatype> ===> TK_INT
<primitiveDatatype> ===> TK_REAL
<constructedDatatype> ===> TK_RECORD TK_RECORDID
<remaining_list> ===> TK_COMMA <parameter_list>
<remaining_list> ===> TK_EPS
<stmts> ===> <typeDefinitions> <declarations> <otherStmts> <returnStmt>
<typeDefinitions> ===> <typeDefinition> <typeDefinitions>
<typeDefinitions> ===> TK_EPS
<typeDefinition> ===> TK_RECORD TK_RECORDID <fieldDefinitions> TK_ENDRECORD TK_SEM
<fieldDefinitions> ===> <fieldDefinition> <fieldDefinition> <moreFields>
<fieldDefinition> ===> TK_TYPE <primitiveDatatype> TK_COLON TK_FIELDID TK_SEM
<moreFields> ===> <fieldDefinition> <moreFields>
<moreFields> ===> TK_EPS
<declarations> ===> <declaration> <declarations>
<declarations> ===> TK_EPS
<declaration> ===> TK_TYPE <dataType> TK_COLON TK_ID TK_COLON <global_or_not> TK_SEM
<global_or_not> ===> TK_GLOBAL
<global_or_not> ===> TK_EPS
<otherStmts> ===> <stmt> <otherStmts>
<otherStmts> ===> TK_EPS
<stmt> ===> <assignmentStmt>
<stmt> ===> <iterativeStmt>
<stmt> ===> <conditionalStmt>
<stmt> ===> <ioStmt>
<stmt> ===> <funCallStmt>
<assignmentStmt> ===> <singleOrRecId> TK_ASSIGNOP <arithmeticExpression> TK_SEM
<singleOrRecId> ===> TK_ID
<singleOrRecId> ===> TK_RECORDID TK_DOT TK_FIELDID
<funCallStmt> ===> <outputParameters> TK_CALL TK_FUNID TK_WITH TK_PARAMETERS <inputParameters>
<outputParameters> ===> TK_SQL <idList> TK_SQR TK_ASSIGNOP
<outputParameters> ===> TK_EPS
<inputParameters> ===> TK_SQL <idList> TK_SQR
<iterativeStmt> ===> TK_WHILE TK_OP <booleanExpression> TK_CL <stmt> <otherStmts> TK_ENDWHILE
<conditionalStmt> ===> TK_IF <booleanExpression> TK_THEN <stmt> <otherStmts> <conditionalSuffix>
<conditionalSuffix> ===> TK_ELSE <otherStmts> TK_ENDIF
<conditionalSuffix> ===> TK_ENDIF
<ioStmt> ===> TK_READ TK_OP <allVar> TK_CL TK_SEM
<ioStmt> ===> TK_WRITE TK_OP <allVar> TK_CL TK_SEM
<allVar> ===> <var>
<allVar> ===> TK_RECORDID
<arithmeticExpression> ===> <arithmeticT> <arithmeticE1>
<arithmeticE1> ===> <operator_plusMinus> <arithmeticT> <arithmeticE1>
<arithmeticE1> ===> TK_EPS
<arithmeticT> ===> <arithmeticF> <arithmeticT1>
<arithmeticT1> ===> <operator_mulDiv> <arithmeticF> <arithmeticT1>
<arithmeticT1> ===> TK_EPS
<arithmeticF> ===> TK_OP <arithmeticExpression> TK_CL
<arithmeticF> ===> <var>
<operator_plusMinus> ===> TK_PLUS
<operator_plusMinus> ===> TK_MINUS
<operator_mulDiv> ===> TK_MUL
<operator_mulDiv> ===> TK_DIV
<booleanExpression> ===> TK_OP <booleanExpression> TK_CL <logicalOp> TK_OP <booleanExpression> TK_CL
<booleanExpression> ===> <var> <relationalOp> <var>
<booleanExpression> ===> TK_NOT <booleanExpression>
<var> ===> TK_ID
<var> ===> TK_NUM
<var> ===> TK_RNUM
<logicalOp> ===> TK_AND
<logicalOp> ===> TK_OR
<relationalOp> ===> TK_LT
<relationalOp> ===> TK_LE
<relationalOp> ===> TK_EQ
<relationalOp> ===> TK_GT
<relationalOp> ===> TK_GE
<relationalOp> ===> TK_NE
<returnStmt> ===> TK_RETURN <optionalReturn> TK_SEM
<optionalReturn> ===> TK_SQL <idList> TK_SQR
<optionalReturn> ===> TK_EPS
<idList> ===> TK_ID <more_ids>
<more_ids> ===> TK_COMMA <idList>
<more_ids> ===> TK_EPS
